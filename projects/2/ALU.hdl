// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=rzx);
    Not16(in=rzx, out=t1);
    Mux16(a=rzx, b=t1, sel=nx, out=rnzx);
    
    Mux16(a=y, b=false, sel=zy, out=rzy);
    Not16(in=rzy, out=t2);
    Mux16(a=rzy, b=t2, sel=ny, out=rnzy);

    Add16(a=rnzx, b=rnzy, out=ad);
    And16(a=rnzx, b=rnzy, out=an);
    Mux16(a=an, b=ad, sel=f, out=t3);

    Not16(in=t3, out=t4);
    Mux16(a=t3, b=t4, sel=no, out=out, out[0..7]=o1, out[8..15]=o2, out[15]=o3);

    Or8Way(in=o1, out=oo1);
    Or8Way(in=o2, out=oo2);
    Or(a=oo1, b=oo2, out=oo3);
    Not(in=oo3, out=zr);
    
    Mux(a=false, b=true, sel=o3, out=ng);
    
}